module Main where
import Control.Applicative ((<*>), (<*), (*>), (<$>))
import Control.Monad
import Control.Monad.Error
import Data.Array
import Data.Char hiding (isSymbol, isNumber)
import Data.Complex
import Data.Ratio
import Data.Typeable
import Numeric
import Text.ParserCombinators.Parsec hiding (spaces)
import System.Environment

data LispVal = Atom String
             | Bool Bool
             | Character Char
             | Complex (Complex Double)
             | Float Double
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | String String
             | Ratio Rational
             | Vector (Array Int LispVal)

showVal :: LispVal -> String
showVal (Atom name) = name
showVal (Bool True) = "#t"
showVal (Bool False) = "#f"
showVal (Character contents) = show contents
showVal (Complex contents) = show contents
showVal (Float contents) = show contents
showVal (Number contents) = show contents
showVal (Ratio contents) = show contents
showVal (String contents) = "\\" ++ contents ++ "\\"
showVal (Vector contents) = show contents
showVal (List contents) = "(" ++ unwordsList contents ++ ")"
showVal (DottedList head tail) = "(" ++ unwordsList head ++ " . " ++ showVal tail ++ ")"

unwordsList :: [LispVal] -> String
unwordsList = unwords . map showVal

instance Show LispVal where show = showVal

eval :: LispVal -> ThrowsError LispVal
eval val@(Atom _) = return val
eval val@(String _) = return val
eval val@(Number _) = return val
eval val@(Bool _) = return val
eval (List [Atom "quote", val]) = return val
eval (List (Atom func : args)) = mapM eval args >>= apply func
eval badForm = throwError $ BadSpecialForm "Unrecognized special form" badForm

data LispError = NumArgs Integer [LispVal]
               | TypeMismatch String LispVal
               | Parser ParseError
               | BadSpecialForm String LispVal
               | NotFunction String String
               | UnboundVar String String
               | Default String

showError :: LispError -> String
showError (UnboundVar message varname) = message ++ ": " ++ varname
showError (BadSpecialForm message form) = message ++ ": " ++ show form
showError (NotFunction message func) = message ++ ": " ++ show func
showError (TypeMismatch expected found) = "Invalid type: expected " ++ expected 
                                          ++ ", found " ++ show found
showError (Parser parseErr) = "Parse error at " ++ show parseErr

instance Show LispError where show = showError
instance Error LispError where
  noMsg = Default "An error has occurred"
  strMsg = Default

type ThrowsError = Either LispError

trapError action = catchError action (return . show)

extractValue :: ThrowsError a -> a 
extractValue (Right val) = val

apply :: String -> [LispVal] -> ThrowsError LispVal
apply func args = maybe (throwError $ NotFunction "Unrecognized primitive function args" func)
                  ($ args)
                  (lookup func primitives)

primitives :: [(String, [LispVal] -> ThrowsError LispVal)]
primitives = [("+", numericBinop (+)),
              ("-", numericBinop (-)),
              ("*", numericBinop (*)),
              ("/", numericBinop div),
              ("mod", numericBinop mod),
              ("quotient", numericBinop quot),
              ("remainder", numericBinop rem),
              ("symbol?", unaryOp isSymbol),
              ("string?", unaryOp isString),
              ("number?", unaryOp isNumber),
              ("string->symbol", unaryOp stringToSymbol),
              ("symbol->string", unaryOp symbolToString)]

isSymbol (Atom _) =  Bool True
isSymbol (_) = Bool False

isString (String _) = Bool True
isString (_) = Bool False 

isNumber (Number _) = Bool True
isNumber (_) = Bool False

stringToSymbol (String val) = Atom val
stringToSymbol _ = error "Expected Atom"

symbolToString (Atom val) = String val
symbolToString notStr = error "Expected String"

unaryOp :: (LispVal -> LispVal) -> [LispVal] -> ThrowsError LispVal
unaryOp f [v] = return $ f v

numericBinop :: (Integer -> Integer -> Integer) -> [LispVal] -> ThrowsError LispVal
numericBinop op [] = throwError $ NumArgs 2 []
numericBinop op singleVal@[_] = throwError $ NumArgs 2 singleVal
numericBinop op params = mapM unpackNum params >>= return . Number . foldl1 op

unpackNum :: LispVal -> ThrowsError Integer
unpackNum (Number n) = return n
unpackNum (String n) = let parsed = reads n in
                        if null parsed
                           then throwError $ TypeMismatch "number" $ String n
                           else return $ fst $ parsed !! 0
unpackNum (List [n]) = unpackNum n
unpackNum notNum = throwError $ TypeMismatch "number" notNum

parseList :: Parser LispVal
parseList = liftM List $ sepBy parseExpr spaces

parseDottedList :: Parser LispVal
parseDottedList = do
  head <- endBy parseExpr spaces
  tail <- char '.' >> spaces >> parseExpr
  return $ DottedList head tail

parseVector :: Parser LispVal
parseVector = do 
  arrayValues <- sepBy parseExpr spaces
  return $ Vector (listArray (0, (length arrayValues - 1)) arrayValues)

parseQuoted :: Parser LispVal
parseQuoted = do
  char '\''
  x <- parseExpr
  return $ List [Atom "quote", x]

parseQuasiQuoted :: Parser LispVal
parseQuasiQuoted = do
  char '`'
  x <- parseExpr
  return $ List [Atom "quasiquote", x]

parseUnquote :: Parser LispVal
parseUnquote = do
  char ','
  x <- parseExpr
  return $ List [Atom "unquote", x]

parseAtom :: Parser LispVal
parseAtom = do 
  first <- letter <|> symbol
  rest <- many (letter <|> digit <|> symbol)
  let atom = first:rest
  return $ (Atom atom)

parseBool :: Parser LispVal
parseBool = do
  char '#'
  (char 't' >> return (Bool True)) <|> (char 'f' >> return (Bool False))

parseChar :: Parser LispVal
parseChar = do
  try $ string "#\\"
  value <- try (string "newline" <|> string "space")
           <|> do { x <- anyChar; notFollowedBy alphaNum; return [x]}
  return $ Character $ case value of 
    "newline" -> '\n'
    "space" -> ' '
    otherwise -> (value !! 0)

toDouble :: LispVal -> Double
toDouble(Float f) = f
toDouble(Number n) = fromIntegral n

parseComplex :: Parser LispVal
parseComplex = do
  x <- (try parseFloat <|> parseDecimal)
  char '+'
  y <- (try parseFloat <|> parseDecimal)
  char 'i'
  return $ Complex (toDouble x :+ toDouble y)

parseRatio :: Parser LispVal
parseRatio = 
  do x <- many1 digit
     char '/'
     y <- many1 digit
     return $ Ratio ((read x) % (read y))

parseFloat :: Parser LispVal
parseFloat = do 
  x <- many1 digit
  char '.'
  y <- many1 digit
  return $ Float (fst . head $ readFloat (x ++ "." ++ y))

parseHex :: Parser LispVal
parseHex = do try $ string "#x"
              digits <- many1 hexDigit
              return $ (Number . fst . head . readHex) digits

parseOct :: Parser LispVal
parseOct  = do try $ string "#o"
               digits <- many1 octDigit
               return $ (Number . fst . head . readOct) digits

parseBin :: Parser LispVal
parseBin = do try $ string "#b"
              digits <- (many1 . oneOf) "01"
              return $ (Number . fst . head . readBin) digits
  where 
    readBin = readInt 2 isBinDigit digitToInt
    isBinDigit x
      | (x == '0') || (x == '1') = True
      | otherwise = False

parseDecimalPrefixed :: Parser LispVal
parseDecimalPrefixed  = do
   try $ string "#d"
   digits <- many1 digit
   return $ (Number . rd) digits
   where rd = read :: String -> Integer

parseDecimal :: Parser LispVal
parseDecimal = many1 digit >>= (\x -> (return . Number . rd) x)
  where rd = read :: String -> Integer

parseNumber :: Parser LispVal
parseNumber = parseHex <|> parseOct <|> parseBin <|> parseDecimalPrefixed <|> parseDecimal

parseEscaped :: Parser Char
parseEscaped = do
  char '\\'
  x <- oneOf "\"\\nrt"
  return $ case x of
    '\\' -> x
    '\"' -> x
    'r' -> '\r'
    'n' -> '\n'
    't' -> '\t'

parseString :: Parser LispVal
parseString = do
  char '"'
  x <- many $ noneOf "\"\\" <|> parseEscaped
  char '"'
  return $ String x

symbol :: Parser Char
symbol = oneOf "!$%&|*+-/:<=>?@^_~"

spaces :: Parser ()
spaces = skipMany1 space

parseExpr :: Parser LispVal
parseExpr = parseAtom
            <|> try parseString
            <|> try parseComplex
            <|> try parseFloat
            <|> try parseRatio
            <|> try parseNumber
            <|> try parseBool
            <|> try parseChar
            <|> try parseQuoted
            <|> try parseQuasiQuoted
            <|> try parseUnquote
            <|> try (do string "#("
                        x <- parseVector
                        char ')'
                        return x)
            <|> do char '('
                   x <- try parseList <|> parseDottedList
                   char ')'
                   return x

readExpr :: String -> ThrowsError LispVal
readExpr input = case parse parseExpr "lisp" input of
  Left err -> throwError $ Parser err
  Right val -> return val

main :: IO ()
main = do
  args <- getArgs
  evaled <- return $ liftM show $ readExpr (args !! 0) >>= eval
  putStrLn $ extractValue $ trapError evaled
